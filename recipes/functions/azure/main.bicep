@description('Information about what resource is calling this Recipe. Generated by Radius.')
param context object

var prefix string = toLower(uniqueString('${context.resource.id}'))
var name string = 'func${prefix}'

// var location = resourceGroup().location
var location = 'eastus'

// Extract connection data from linked resources
var resourceConnections = context.resource.connections ?? {}

// Build environment variables from connections unless explicitly disabled via disableDefaultEnvVars
// Each connection's output values become CONNECTION_<CONNECTION_NAME>_<PROPERTY_NAME>
var connectionEnvVars = reduce(items(resourceConnections), [], (acc, conn) => 
  conn.value.?disableDefaultEnvVars != true
    ? concat(acc, reduce(items(conn.value.?status.?computedValues ?? {}), [], (envAcc, prop) => 
        concat(envAcc, [{
          name: toUpper('CONNECTION_${conn.key}_${prop.key}')
          value: string(prop.value)
        }])
      ))
    : acc
)

resource storage 'Microsoft.Storage/storageAccounts@2022-09-01' = {
  name: '${name}storage'
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
  properties: {
    supportsHttpsTrafficOnly: true
    minimumTlsVersion: 'TLS1_2'
    accessTier: 'Hot'
  }
}

resource plan 'Microsoft.Web/serverfarms@2023-12-01' = {
  name: '${name}plan'
  location: location
  kind: 'functionapp'
  sku: {
    name: 'EP1'
    tier: 'ElasticPremium'
    size: 'EP1'
    family: 'EP'
    capacity: 1
  }
  properties: {
    reserved: true
  }
}

resource functionApp 'Microsoft.Web/sites@2023-12-01' = {
  name: '${name}func'
  location: location
  kind: 'functionapp,linux'
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    serverFarmId: plan.id
    httpsOnly: true
    siteConfig: {
      linuxFxVersion: 'DOCKER|${context.resource.properties.image}'
      alwaysOn: true
      appSettings: concat([
        {
          name: 'FUNCTIONS_WORKER_RUNTIME'
          value: 'node'
        }
        {
          name: 'FUNCTIONS_EXTENSION_VERSION'
          value: '~4'
        }
        {
          name: 'AzureWebJobsStorage'
          value: 'DefaultEndpointsProtocol=https;AccountName=${storage.name};AccountKey=${storage.listKeys().keys[0].value};EndpointSuffix=${environment().suffixes.storage}'
        }
        {
          name: 'WEBSITES_ENABLE_APP_SERVICE_STORAGE'
          value: 'false'
        }
        {
          name: 'WEBSITES_PORT'
          value: '80'
        }
        {
          name: 'TEST1'
          value: 'TEST2'
        }
      ], connectionEnvVars)
    }
  }
}

output result object = {
  values: {
    function_url: 'https://${functionApp.properties.defaultHostName}/${context.resource.properties.functionPath}'
  } 
}
